group 'se.adolfsson.webtaint'
version '1.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'com.github.johnrengelman.plugin-shadow'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'
    compile group: 'org.javassist', name: 'javassist', version: '3.22.0-GA'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    archiveName = "${rootProject.name}-${rootProject.version}.jar"
    manifest {
        attributes(
                'Premain-Class': 'se.adolfsson.webtaint.agent.TaintAgent',
                'Agent-Class': 'se.adolfsson.webtaint.agent.TaintAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "webtaint",
                'Implementation-Version': rootProject.version
        )
    }
}

task rtCompile(type: JavaExec, dependsOn: compileJava) {
    main 'se.adolfsson.webtaint.xboot.TaintFieldAdder'
    classpath sourceSets.main.runtimeClasspath
}

task rtJar(type: Jar, dependsOn: rtCompile) {
    from 'build/taint'
    baseName 'webtaint-rt'
}

shadowJar {
    baseName 'webtaint-agent'
    classifier ''
}

artifacts {
    archives rtJar
    archives shadowJar
}
