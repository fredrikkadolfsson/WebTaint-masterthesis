group 'se.adolfsson.dtp'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'com.github.johnrengelman.plugin-shadow'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'
    compile group: 'org.javassist', name: 'javassist', version: '3.22.0-GA'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    archiveName = "${rootProject.name}-${rootProject.version}.jar"
    manifest {
        attributes(
                'Premain-Class': 'se.adolfsson.dtp.TaintAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "dtp",
                'Implementation-Version': rootProject.version
        )
    }
}

task rtCompile(type: JavaExec, dependsOn: compileJava) {
    main 'se.adolfsson.dtp.pcm.TaintFieldAdder'
    classpath sourceSets.main.runtimeClasspath
}

task rtJar(type: Jar, dependsOn: rtCompile) {
    from 'build/taint'
    baseName 'dtp-rt'
}

shadowJar {
    archiveName = "${rootProject.name}-agent.jar"
    classifier 'agent'
}

artifacts {
    archives rtJar
    archives shadowJar
}
